plugins {
    id 'java'
    id 'java-library'
}

dependencies {
    api "org.spongepowered:mixin:${project.mixin_version}"

    api "org.apache.logging.log4j:log4j-api:${project.log4j_version}"
    api "org.apache.logging.log4j:log4j-core:${project.log4j_version}"
    api "org.apache.logging.log4j:log4j-slf4j18-impl:${project.log4j_version}"

    api "com.google.guava:guava:${project.guava_version}"

    api "org.ow2.asm:asm-tree:${project.asm_version}"
    api "org.ow2.asm:asm-commons:${project.asm_version}"
    api "org.ow2.asm:asm-util:${project.asm_version}"

    api "commons-io:commons-io:${project.commons_io_version}"

    api "com.google.code.gson:gson:${project.gson_version}"

    api "net.minecraftforge:forgeflower:${project.forgeflower_version}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junit_version}"
    testImplementation  "org.junit.jupiter:junit-jupiter-params:${project.junit_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${project.junit_version}"
}

test {
    useJUnitPlatform()
    forkEvery(1)
}

// Setup testing environment
// Create test target jars first.
def executorProject = project
def diggerProjects = rootProject.allprojects.findAll{project -> rootProject.projectDir.toPath().relativize(project.projectDir.toPath()).toString().startsWith("Digger")}
diggerProjects.forEach {diggerProject ->

    diggerProject.getPlugins().apply("java")
    diggerProject.getPlugins().apply("application")

    diggerProject.version = "1.0"

    diggerProject.tasks.create("setupTestEnvironment", Copy) { setupTask ->
        setupTask.from diggerProject.projectDir.toPath().resolve("build/libs").toFile()
        setupTask.into executorProject.projectDir.toPath().resolve("src/test/resources/runtimes/${diggerProject.name}").toFile()

        diggerProject.afterEvaluate {
            setupTask.dependsOn diggerProject.tasks.getByName("build")
        }

        setupTask.description = "Copy ${diggerProject.name} jars to executors test resources"
        setupTask.group = "verification"
    }

    if (diggerProject.name.endsWith("- Mixin")) {
        var projectId = diggerProject.name.replace(" - Mixin", "").toLowerCase()

        //Mixin project
        diggerProject.dependencies {
            implementation project(":${diggerProject.name.replace("Mixin", "Source")}")
            implementation "org.spongepowered:mixin:${project.mixin_version}"
        }

        diggerProject.jar {it ->
            it.manifest { manifest ->
                manifest.attributes([
                        "Automatic-Module-Name"   : "${projectId}",
                        "MixinConfigs": "test.mixins.json"
                ])
            }
        }
    }
}

tasks.withType(Test).configureEach {testTask ->
    diggerProjects.forEach {diggerProject ->
        testTask.dependsOn diggerProject.tasks.getByName("setupTestEnvironment")
    }

    doFirst {
        def outputDirectory = project.file("src/test/output");
        if (outputDirectory.exists()) {
            outputDirectory.delete();
        }
    }
}

tasks.clean.doLast {
    def testRuntimesDirectory = project.file("src/test/resources/runtimes");
    if (testRuntimesDirectory.exists()) {
        deleteFolder(testRuntimesDirectory)
    }

    def outputDirectory = project.file("src/test/output");
    if (outputDirectory.exists()) {
        deleteFolder(outputDirectory)
    }
}

static void deleteFolder(File folder) {
    File[] files = folder.listFiles();
    if(files!=null) { //some JVMs return null for empty dirs
        for(File f: files) {
            if(f.isDirectory()) {
                deleteFolder(f);
            } else {
                f.delete();
            }
        }
    }
    folder.delete();
}